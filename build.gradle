plugins {
    id 'fabric-loom' version '1.8-SNAPSHOT'
    id 'maven-publish'
}

version = project.mod_version + extraVersion()
group = project.maven_group

def extraVersion() {
    def ENV = System.getenv()
    if (ENV.CI == "true") {
        def branch = ENV.GITHUB_REF
        branch = branch.substring(branch.lastIndexOf("/") + 1)
        def runNumber = ENV.GITHUB_RUN_NUMBER
        return "-pre+" + runNumber + "-" + branch
    }
    return "+${project.minecraft_version}"
}

repositories {
    mavenCentral()
    maven { url "https://maven.shedaniel.me/" }
    maven { url "https://maven.andante.dev/releases/" }
    maven { url 'https://jitpack.io' }
    maven { url "https://maven.terraformersmc.com/releases/" }
    maven { url = "https://maven.isxander.dev/releases" }
    maven { url = "https://maven.isxander.dev/snapshots" }
    maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url = uri("https://maven.noxcrew.com/public") }
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:$minecraft_version"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:$loader_version"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:$fabric_version"
    modApi("dev.isxander:yet-another-config-lib:$yacl_version-fabric") {
        exclude(group: "com.twelvemonkeys.common")
        exclude(group: "com.twelvemonkeys.imageio")
    }

    modApi("com.terraformersmc:modmenu:$modmenu_version")

    implementation include("com.github.JnCrMx:discord-game-sdk4j:v$discord_game_sdk_version")
    modApi("com.noxcrew.noxesium:fabric:$noxesium_version") {
        exclude(group: "org.khelekore")
    }
}

processResources {
    inputs.property "version", project.version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

def targetJavaVersion = 21
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release = targetJavaVersion
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    archivesBaseName = project.archives_base_name
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

loom {
    accessWidenerPath = file("src/main/resources/islandutils.accesswidener")
}
